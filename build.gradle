plugins {
    id 'com.palantir.git-version' version '0.12.3'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'com.github.jk1.dependency-license-report' version '1.14'
    id 'org.sonarqube' version '3.3'
    id 'jacoco'
}

apply plugin: 'idea'

group = 'engineering.everest.starterkit'
sourceCompatibility = '1.11'

def gitTagVersion = versionDetails()
version = gitTagVersion.commitDistance == 0
        ? gitTagVersion.lastTag
        : "${gitTagVersion.lastTag}+${gitTagVersion.commitDistance}-SNAPSHOT"

ext {
    lombokVersion = '1.18.20'
    springBootVersion = '2.4.8'

    junitVersion = '5.7.2'
    mockitoVersion = '3.11.2'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
    }
}

dependencies {
    api "org.springframework.boot:spring-boot-starter-security"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure:${springBootVersion}"
    implementation "org.springframework.security:spring-security-config"

    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.security:spring-security-test"
}

java {
    java {
        withJavadocJar()
        withSourcesJar()
    }
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

apply from: 'publishing.gradle'
apply from: 'sonar.gradle'
