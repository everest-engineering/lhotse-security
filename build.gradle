plugins {
	id 'java'
	id 'io.freefair.lombok' version '4.1.6'
	id 'maven-publish'
	id "com.jfrog.bintray" version "1.8.4"
    id 'fr.brouillard.oss.gradle.jgitver' version '0.9.1'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
}


apply plugin: 'io.freefair.lombok'
apply plugin: 'idea'
apply plugin: 'java-library'
apply plugin: 'io.spring.dependency-management'

repositories {
    mavenCentral()
}

group = 'engineering.everest.starterkit'
sourceCompatibility = '1.11'

test {
	useJUnitPlatform()
}

lombok {
    version = "1.18.12"
    generateLombokConfig.enabled = false
}

jgitver {
    autoIncrementPatch = false
    useDirty = true
}

ext {
    springBootVersion = '2.2.5.RELEASE'

    junitVersion = '5.6.0'
    mockitoVersion = '3.2.4'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
    }
}

dependencies {
    api "org.springframework.boot:spring-boot-starter-security"

    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure:${springBootVersion}"
    implementation "org.springframework.security:spring-security-config"

    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.security:spring-security-test"
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['maven']
    pkg {
        repo = 'starterkit'
        name = rootProject.name
        userOrg = 'everestengineering'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/everestengineering/starterkit-storage'
    }
    publish = true
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}